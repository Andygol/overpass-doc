Формати даних
=============

Для обміну даними OpenStreetMap існує кілька форматів даних.
Тут ви знайдете більшість з них з готовими до застосування рецептами.

<a name="scope"/>
## Область застосування

Про типи даних вже була згадка у відповідному [розділі](../preface/osm_data_model.md) у Вступі.
Отже, ви вже знаєте про _node_, _way_ та _relation_.

Доволі частою проблемою є отримання геометрії об'єктів OpenStreetMap,
її розв'язання розглядається в [розділі про геометрію об'єктів](../full_data/osm_types.md) в главі [Отримання просторових даних](../full_data/index.md).

<a name="faithful"/>
## Традиційні ступені деталізації

Почнемо зі ступенів деталізації:
хоча загальний формат виводу контролюється глобальним налаштуванням для кожного запиту,
ступінь деталізації виводу встановлюється для кожного виразу відповідними параметрами.
Таким чином в одному запиті можна поєднувати різний ступінь деталізації для його частин.
Це можливість потрібна для отримання оптимального обсягу даних для [деяких варіантів геометрії](../full_data/osm_types.md#full).
На додачу, кожен [застосунок](index.md) отримує інформацію про найкращий спосіб виводу даних.

Розглянемо кожен ступінь деталізації на прикладі передмістя Гринвіч у Лондоні.
Усі приклади створені таким чином, щоб повернути досить невелику кількість точок, ліній та зв'язків
для їх перегляду у вкладці _Data_ в Overpass Turbo.

Для початкових ступенів деталізації OpenStreetMap існує ієрархія їх використання:

Вираз _out ids_ [поверне](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB):

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out ids;

* ідентифікатори об'єктів

Вираз _out skel_ [поверне](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB), на додачу до основних даних, дані потрібні для відновлення геометрії:

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out skel;

* для точок - їх координати
* для ліній та зв'язків - перелік їх членів

Вираз _out_ (без параметрів) [поверне](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB) повні геодані на додачу до:

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out;

* теґів всіх об'єктів

Вираз _out meta_ [поверне](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB) на додачу:

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out meta;

* версії об'єктів
* та відбитки часу

Нарешті, вираз _out attribution_ повертає наступні дані:

* id набору змін
* id мапера
* та призвісько мапера, що пов'язане з id

Ця остання ступінь деталізації даних підпадає,
відповідно до прийнятих в спільноті домовленостей,
до розряду питань конфіденційності.
Тому тут існують певні [перепони](../analysis/index.md) на шляху отримання цих даних.
Оскільки жодні з цих даних не потрібні для випадків, які розглядаються у цьому розділі,
ми утримуємось від наведення прикладу тут.

<a name="extras"/>
## Додаткові дані

У вас є можливість зробити вивід трьома різними способами для отримання даних про геометрію.
Можливі будь-які комбінації між наведеними рівнями деталізації.

Параметр _center_ використовується для виводу координат центру кожного об'єкта,
Ці координати не мають будь-якого складного математичного значення,
вони показують середину прямокутника, який можна окреслити навколо кожного об'єкта: 
[приклад 1](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB)

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out ids center;

[приклад 2](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB)

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out center;

Параметр _bb_ (_bounding box_) використовується для показу прямокутників, що містять кожну лінію чи зв'язок:
[приклад](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB)

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out ids bb;

Параметр _geom_ (_geometry_) долучає до ліній та зв'язків координати точок з яких вони складаються.
Для цього рівень деталізації має бути принаймні _skel_,
він працює й для інших до _attribution_ включно:
[example](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB)

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out skel geom;

І ось ми отримали не лише кількасот метрів в парку біля Гринвічу,
а й кількасот кілометрів пішохідних шляхів у східній Англії.
Це загальна проблема зв'язків.
Як один зі способів її розв'язання можна використовувати обмежувальний прямокутник для обмеження виводу ([див. докладніше](../full_data/bbox.md#crop)).

І на останок, існує формат _tags_.
Він базується на _ids_ та показує додатково всі теґи об'єктів, але не їх геометрію чи складові.
Перш за все, це корисно, якщо вам результаті [не потрібні координати](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB):

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out tags;

Його також можна [поєднати](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB) з двома параметрами, що повертають координати: _center_ та _bb_:

    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out tags center;

<a name="json"/>
## JSON та GeoJSON

Тепер перейдемо до форматів даних:
Хоча ступінь деталізації можна вибрати для кожної команди виведення,
вихідний формат може бути оголошений лише глобально на початку запита.
Крім того, вибір формату виведення змінює лише форму, але не зміст.

З форматом JOSN  ми опиняємось посередині конфлікту.
З одного боку, це існує досить популярний формат JSON для геоданих - GeoJSON.
З іншого боку, OpenStreetMap повинен зберігати свою структуру,
і ця структура не вписується в модель даних GeoJSON.

Як рішення, є можливість створити об'єкти GeoJSON з об'єктів OpenStreetMap.
Хоча, оригінальні об'єкти OpenStreetMap правдиво представляються в JSON, а не в GeoJSON.

Об'єкти OpenStreetMap [в JSON](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB):

    [out:json];
    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out geom;

Похідні об'єкти [в GeoJSON](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB):

    [out:json];
    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    convert item ::=::,::geom=geom(),_osm_type=type();
    out geom;

Про створення похідних об'єктів докладніше йдеться у відповідному [розділі](../counting/index.md).

<a name="csv"/>
## CSV

Можливість організовувати дані у вигляді таблиці часто буває корисним.
В контексті OpenStreetMap це означає, що в стовпцях будуть теґи обрані користувачем, а кожний рядок буде представляти знайдений об'єкт.

Вибір стовпців належним чином обмежує доступну інформацію про об’єкт для більшості випадків.
Так, теґи що не були зазначені як стовпці відкидаються при виводі.
Геометрії, складніші за одну координату, також не можна виводити у цьому форматі.
Це відмінність від форматів XML та JSON.

В стандартному випадку назва стовпця це ключ теґу.
В значеннях стовпців виводяться значення теґу для зазначених ключів об'єкта.
Якщо в об'єкта немає такого ключа - виводиться пусте значення.
Для стандартних властивостей об'єкта використовуються стовпці зі спеціальними назвами,
які починаються з `::`:
[приклад](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB)

    [out:csv(::type,::id,name)];
    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out center;

Абревіатура CSV походить від _comma separated value_.
Але різні програми, що послуговуються цим форматом, мають різні очікування,
щодо символу що використовується як роздільник.
Тож, є можливість вказати який символ використовувати як роздільник та
[чи показувати заголовний рядок, чи ні](https://overpass-turbo.eu/?lat=51.4775&lon=0.0&zoom=16&Q=CGI_STUB):

    [out:csv(::type,::id,name;false;"|")];
    ( way(51.477,-0.001,51.478,0.001)[name="Blackheath Avenue"];
      node(w);
      relation(51.477,-0.001,51.478,0.001); );
    out center;

Для [кожного застосунку](index.md) він показує слушний варіант.
